// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack
/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {
    static Array ram;
    static Array heap;
    static int freeList; // PAYLOAD address of first free block (or 0)

    /** Initializes the class. */
    function void init() {
        let ram = 0; // RAM base address
        let heap = 2048; // heap base (header of first free block)
        let heap[0] = 0; // header field: next(index) = 0   (at 2048)
        let heap[1] = 14334; //  header field: size do not include header (at 2049)
        let freeList = 2; // holds the INDEX (relative to heap) of the first free payload. I used indexes to simplify pointer arithmetic;
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return ram[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let ram[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function Array alloc(int size) {
        var int prev, curr, next, sz, newSize;

        // Walk the free list, looking for a block of sufficient size
        let prev = 0; // no previous block
        let curr = freeList;
        while (curr > 0) {
            let sz = Memory.getSize(curr);
            if (sz > (size + 1)) { // found a block of sufficient size(save 2 for block header)
                // Found a block
                let newSize = sz - (size + 2);
                if (newSize = 0) {
                    let next = Memory.getNext(curr);
                } else {
                    let next = curr + size + 2;
                    do Memory.setSize(next, newSize);
                    do Memory.setNext(next, Memory.getNext(curr));
                    do Memory.setSize(curr, size);
                }
                if (prev = 0) { let freeList = next; }
                else { do Memory.setNext(prev, next); }
                do Memory.setNext(curr, 0);
                return curr+heap; // return the address
            }
            let prev = curr;
            let curr = Memory.getNext(curr);
        }
        return 0; // no block of sufficient size was found
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        var int tail,o_idx;
        let o_idx = o - heap; // convert address to index
        // empty list -> o becomes head
        if (freeList = 0) {
            let freeList = o_idx;
            return;
        }
        
        // Walk to the end of the free list
        let tail = freeList;
        while (Memory.getNext(tail) > 0) {
            let tail = Memory.getNext(tail);
        }
        // Append this block
        do Memory.setNext(tail, o_idx);
        return;
    }
    function int getNext(int i) {
        return heap[i-2];
    }

    function void setNext(int i, int j) {
        let heap[i-2] = j;
        return;
    }

    function int getSize(int i) {
        return heap[i-1];
    }

    function void setSize(int i, int size) {
        let heap[i-1] = size;
        return;
    }
}
